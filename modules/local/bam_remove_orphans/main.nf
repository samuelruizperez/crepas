
/*
 * Remove orphan reads from paired-end BAM file
 * Downloaded from: https://github.com/nf-core/chipseq/blob/76e2382b6d443db4dc2396e6831d1243256d80b0/modules/local/bam_remove_orphans.nf
 * Adapted by Samuel Ruiz-Pérez <samper@cancer.dk>.
 */
process BAM_REMOVE_ORPHANS {
    tag "$meta.id"
    label 'process_medium'

    conda "${moduleDir}/environment.yml"
    container "${ workflow.containerEngine == 'singularity' && !task.ext.singularity_pull_docker_container ?
        'https://depot.galaxyproject.org/singularity/mulled-v2-57736af1eb98c01010848572c9fec9fff6ffaafd:402e865b8f6af2f3e58c6fc8d57127ff0144b2c7-0' :
        'biocontainers/mulled-v2-57736af1eb98c01010848572c9fec9fff6ffaafd:402e865b8f6af2f3e58c6fc8d57127ff0144b2c7-0' }"

    input:
    tuple val(meta), path(bam)
    val skip_name_sort 

    output:
    tuple val(meta), path("${prefix}.bam"), emit: bam
    path "versions.yml"                   , emit: versions

    when:
    task.ext.when == null || task.ext.when

    script:
    def args = task.ext.args ?: ''
    prefix   = task.ext.prefix ?: "${meta.id}"
    if (!meta.single_end) {

        if (!skip_name_sort) {
            """
            if [ "\$(samtools view -c ${bam})" -eq 0 ]; then
                ln -s ${bam} ${prefix}.bam
            else
                samtools sort -n -@ ${task.cpus} -o ${prefix}.nsorted.bam -T ${prefix}.nsorted ${bam}
                bampe_rm_orphan.py ${prefix}.nsorted.bam ${prefix}.bam ${args}
            fi

            cat <<-END_VERSIONS > versions.yml
            "${task.process}":
                samtools: \$(echo \$(samtools --version 2>&1) | sed 's/^.*samtools //; s/Using.*\$//')
            END_VERSIONS
            """
        } else {
            """
            if [ "\$(samtools view -c ${bam})" -eq 0 ]; then
                ln -s ${bam} ${prefix}.bam
            else
                bampe_rm_orphan.py ${bam} ${prefix}.bam ${args}
            fi

            cat <<-END_VERSIONS > versions.yml
            "${task.process}":
                samtools: \$(echo \$(samtools --version 2>&1) | sed 's/^.*samtools //; s/Using.*\$//')
            END_VERSIONS
            """
        }      

    } else {
        """
        ln -s ${bam} ${prefix}.bam

        cat <<-END_VERSIONS > versions.yml
        "${task.process}":
            samtools: \$(echo \$(samtools --version 2>&1) | sed 's/^.*samtools //; s/Using.*\$//')
        END_VERSIONS
        """
    }

    stub:
    prefix = task.ext.prefix ?: "${meta.id}"
    """
    touch ${prefix}.bam

    cat <<-END_VERSIONS > versions.yml
    "${task.process}":
        samtools: \$(echo \$(samtools --version 2>&1) | sed 's/^.*samtools //; s/Using.*\$//')
    END_VERSIONS
    """
}